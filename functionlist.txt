const PI = 3.14159265358979323846264338327950288419716939937510582097494459230781640628620899862803482534211706798214808651328230664709384460955058223172535
const e = 2.71828182845904523536028747135266249775724709369995


Function Tand(X)
	Tand = Tan(Rad(X))
End Function

Function Sind(X)
	Sind = sin(Rad(X))
End Function

Function Cosd(X)
	Cosd = cos(Rad(X))
End Function

Function Rad(X)
	Rad = X * Atn(1) / 45
End Function

Function Deg(x)
  Deg = x * 45 / Atn(1)
End Function

Function asn(X)
	if x = 1 then asn = pi /2: exit function
	asn = Atn(X / Sqr(-X * X + 1))
End Function

Function acs(X)
	acs = Atn(-X / Sqr(-X * X + 1)) + 2 * Atn(1)
End Function

function binary(b)
	binary = binaryC(b, 30)
end function

function binaryC(b, m)
	Dim i, bin, x, maxpower
	
	maxpower = m-1
	
	bin = ""  'Build the desired binary number in this string, bin.
	x = cdbl(b) 'Convert decimal string in text1 to long integer
	
	If x < 0 Then bin = bin + "1 " Else bin = bin + "0 "
	
	For i = maxpower To 0 Step -1
		If x And (2 ^ i) Then   ' Use the logical "AND" operator.
			bin = bin + "1"
		Else
			bin = bin + "0"
		End If
	Next
	
	binaryC = bin
end function


function LongToRGB(byval c)
	dim r,g,b
	
	r = c Mod &H100
	c = c \ &H100
	g = c Mod &H100
	c = c \ &H100
	b = c Mod &H100
	LongToRGB = "rgb(" & r & ", " & g & ", " & b & ")"
End function

Function Factorial(a)
	x=1
	For i=1 to a
	  x=x*i
	next 
	Factorial=x
End Function

Function IIf(bClause, sTrue, sFalse)
    If CBool(bClause) Then
        IIf = sTrue
    Else 
        IIf = sFalse
    End If
End Function


Function printTime(lSeconds)
	Dim lMinutes, lHours, lDays
	lDays = lSeconds \ 86400
	lSeconds = lSeconds - lDays * 86400

	lHours = lSeconds \ 3600
	lSeconds = lSeconds - lHours * 3600

	lMinutes = lSeconds \ 60
	lSeconds = lSeconds - lMinutes * 60

	printTime = IIf(lDays >= 10, lDays, "0" & lDays) & " " & _
			 IIf(lHours >= 10, lHours, "0" & lHours) & ":" & _
			 IIf(lMinutes >= 10, lMinutes, "0" & lMinutes) & ":" & _
			 IIf(lSeconds >= 10, lSeconds, "0" & lSeconds)
End Function


function str2char(sInput)
	dim i, sOut
	
	sOut = "char str[] = {"
	
	for i = 1 to len(sInput)
		if i > 1 then 
			sOut = sOut & ","
		end if
		
		sOut = sOut & "'" & mid(sInput,i,1) & "'"
	next
	
	str2char = sOut & "};"
end function

function escape(sInput)
	dim i, sOut, t
	
	for i = 1 to len(sInput)
		t = mid(sInput,i,1)
		if t = "\" or t = """" then 
			sOut = sOut & "\" & t
		else
			sOut = sOut & t
		end if
		
	next
	
	escape = sOut
end function

function increase(start, added, increments)
	dim i, j, s
	j = start
	for i = 0 to increments - 1
		j = j + int(added ^ (i / 1.8))
		s = s & ", " & j
	next
	
	increase = s
end function

Function Val( myString )
' Val Function for VBScript (aka ParseInt Function in VBScript).
' By Denis St-Pierre.
' Natively VBScript has no function to extract numbers from a string.
' Based shamelessly on MS' Helpfile example on RegExp object.
' CAVEAT: Returns only the *last* match found
'         (or, with objRE.Global = False, only the *first* match)

    Dim colMatches, objMatch, objRE, strPattern

    ' Default if no numbers are found
    Val = 0

    strPattern = "[-+0-9]+"       ' Numbers positive and negative; use
                                  ' "Ë†[-+0-9]+" to emulate Rexx' Value()
                                  ' function, which returns 0 unless the
                                  ' string starts with a number or sign.
    Set objRE = New RegExp        ' Create regular expression object.
    objRE.Pattern    = strPattern ' Set pattern.
    objRE.IgnoreCase = True       ' Set case insensitivity.
    objRE.Global     = True       ' Set global applicability:
                                  '   True  => return last match only,
                                  '   False => return first match only.
    Set colMatches = objRE.Execute( myString )  ' Execute search.
    For Each objMatch In colMatches             ' Iterate Matches collection.
        Val = objMatch.Value
    Next
    Set objRE= Nothing
End Function

function stopwatch(answer)
	dim a, s
	a = winapi.gettickcount()
	s = split(answer," ")(2)
	stopwatch = printtime(round((a-val(s))/1000)) & " " & a
	
end function


'Converts capacity in bytes to a readable format like 100.4KB
function capacity(inp)
	dim capacityNames
	dim i
	
	capacityNames = split(",K,M,G,T,P,E,Z,Y,B,GEOP", ",")
	
	i = 0
	
	while(inp > 1024 and i < ubound(capacityNames))
		inp = inp / 1024
		i = i + 1
	wend
	
	capacity = round(inp,2) & capacityNames(i) & "B"

end function



function uniquestring(inp)
	dim st, i
	
	for i = 1 to len(inp)
		if instr(1, st, mid(inp,i,1)) = 0 then
			st = st & mid(inp,i,1)
		end if
	next
	
	uniquestring = st
end function